buildscript {
  ext.versions = [
      'supportLibrary': '27.0.2',
      'kotlin': '1.2.10'
  ]

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.0.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.squareup.sqldelight:gradle-plugin:0.6.1'
  }
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.squareup.sqldelight'

android {
  compileSdkVersion 27

  defaultConfig {
    applicationId 'com.jakewharton.sdksearch'

    minSdkVersion 24
    targetSdkVersion 27

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  signingConfigs {
    debug {
      storeFile rootProject.file('debug.keystore')
      storePassword 'sdksearch'
      keyAlias 'sdksearch'
      keyPassword 'sdksearch'
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      signingConfig signingConfigs.debug
    }
  }
}

repositories {
  mavenCentral()
  google()
  jcenter()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"

  compile "com.android.support:support-annotations:${versions.supportLibrary}"
  compile "com.android.support:support-v4:${versions.supportLibrary}"
  compile "com.android.support:appcompat-v7:${versions.supportLibrary}"
  compile "com.android.support:design:${versions.supportLibrary}"
  compile "com.android.support:recyclerview-v7:${versions.supportLibrary}"
  compile "com.android.support:customtabs:${versions.supportLibrary}"
  compile 'com.android.support.constraint:constraint-layout:1.0.2'

  compile 'com.squareup.sqlbrite3:sqlbrite-kotlin:3.0.0'
  compile 'android.arch.persistence:db-framework:1.0.0'

  kapt 'com.google.auto.value:auto-value:1.5.2'
  compileOnly 'com.jakewharton.auto.value:auto-value-annotations:1.5'

  compile 'com.squareup.okio:okio:1.13.0'

  compile 'com.squareup.moshi:moshi:1.5.0'

  compile 'com.squareup.okhttp3:okhttp:3.9.1'
  compile 'com.squareup.okhttp3:logging-interceptor:3.9.1'

  compile 'com.squareup.retrofit2:retrofit:2.3.0'
  compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
  compile 'com.squareup.retrofit2:converter-moshi:2.3.0'

  compile 'io.reactivex.rxjava2:rxjava:2.1.6'
  compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
  compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
  compile 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.0.0'
  compile 'com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:2.0.0'

  compile 'com.jakewharton.timber:timber:4.6.0'

  compile 'com.google.dagger:dagger:2.13'
  kapt 'com.google.dagger:dagger-compiler:2.13'

  compile 'com.jakewharton:butterknife:8.8.1'
  kapt 'com.jakewharton:butterknife-compiler:8.8.1'
}

// Force all of the primary support libraries to use the same version.
configurations.all {
  resolutionStrategy {
    eachDependency { details ->
      if (details.requested.group == 'com.android.support') {
        details.useVersion versions.supportLibrary
      }
    }
  }
}
