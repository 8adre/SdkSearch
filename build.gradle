buildscript {
  ext.buildConfig = [
      'compileSdk': 29,
      'minSdk': 24,
      'targetSdk': 29,

      'version': [
          'major': 1,
          'minor': 4,
          'patch': 1,
          'build': 0,
      ],
  ]
  ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
  ext.buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
  ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

  ext.versions = [
      'kotlin': '1.3.41',
      'kotlinCoroutines': '1.3.0-M2',
      'kotlinSerialization': '0.11.1',
      'ktor': '1.2.1',
      'okhttp': '4.0.0',
      'retrofit': '2.6.0',
      'dagger': '2.23.1',
      'sqldelight': '1.1.4',
  ]

  ext.deps = [
      'kotlin': [
          'stdlib': [
              'common': "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin}",
              'jdk': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
              'js': "org.jetbrains.kotlin:kotlin-stdlib-js:${versions.kotlin}",
          ],
          'test': [
              'common': "org.jetbrains.kotlin:kotlin-test-common:${versions.kotlin}",
              'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common:${versions.kotlin}",
              'jdk': "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}",
              'js': "org.jetbrains.kotlin:kotlin-test-js:${versions.kotlin}",
          ],
          'coroutines': [
              'common': "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:${versions.kotlinCoroutines}",
              'jdk': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinCoroutines}",
              'jdk8': "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:${versions.kotlinCoroutines}",
              'js': "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:${versions.kotlinCoroutines}",
              'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinCoroutines}",
          ],
          'serialization': [
              'common': "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:${versions.kotlinSerialization}",
              'jdk': "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${versions.kotlinSerialization}",
              'js': "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:${versions.kotlinSerialization}",
          ],
      ],
      'androidx': [
          'appCompat': 'androidx.appcompat:appcompat:1.0.0',
          'browser': 'androidx.browser:browser:1.0.0',
          'constraintLayout': 'androidx.constraintlayout:constraintlayout:1.1.3',
          'core': 'androidx.core:core-ktx:1.0.0',
          'recyclerView': 'androidx.recyclerview:recyclerview:1.1.0-alpha06',
          'sqliteFramework': 'androidx.sqlite:sqlite-framework:1.0.0',
          'test': [
              'runner': 'androidx.test:runner:1.2.0',
              'rules': 'androidx.test:rules:1.2.0',
          ],
      ],
      'material': 'com.google.android.material:material:1.0.0',
      'okhttp': [
          'client': "com.squareup.okhttp3:okhttp:${versions.okhttp}",
          'logging': "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}",
          'mockWebServer': "com.squareup.okhttp3:mockwebserver:${versions.okhttp}",
      ],
      'retrofit': [
          'client': "com.squareup.retrofit2:retrofit:${versions.retrofit}",
          'converterKotlinxSerialization': 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.4.0',
      ],
      'dagger': [
          'runtime': "com.google.dagger:dagger:${versions.dagger}",
          'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
          'androidRuntime': "com.google.dagger:dagger-android:${versions.dagger}",
          'androidCompiler': "com.google.dagger:dagger-android-processor:${versions.dagger}",
      ],
      'okio': 'com.squareup.okio:okio:2.2.2',
      'moshi': 'com.squareup.moshi:moshi:1.8.0',
      'timber': [
          'common': 'com.jakewharton.timber:timber-common:5.0.0-SNAPSHOT',
          'jdk': 'com.jakewharton.timber:timber-jdk:5.0.0-SNAPSHOT',
          'js': 'com.jakewharton.timber:timber-js:5.0.0-SNAPSHOT',
          'android': 'com.jakewharton.timber:timber-android:5.0.0-SNAPSHOT',
      ],
      'junit': 'junit:junit:4.12',
      'truth': 'com.google.truth:truth:0.45',
      'bugsnag': 'com.bugsnag:bugsnag-android:4.11.0',
      'argparser': 'com.xenomachina:kotlin-argparser:2.0.5',
      'byteunits': 'com.jakewharton.byteunits:byteunits:0.9.1',
      'sqldelight': [
          'android': "com.squareup.sqldelight:android-driver:${versions.sqldelight}",
          'coroutines': "com.squareup.sqldelight:coroutines-extensions-jvm:${versions.sqldelight}",
          'runtime': [
              'common': "com.squareup.sqldelight:runtime:${versions.sqldelight}",
              'jdk': "com.squareup.sqldelight:runtime-jvm:${versions.sqldelight}",
              'js': "com.squareup.sqldelight:runtime-js:${versions.sqldelight}",
          ],
      ],
      'ktor': [
          'core': "io.ktor:ktor-server-core:${versions.ktor}",
          'netty': "io.ktor:ktor-server-netty:${versions.ktor}",
      ],
      'logback': 'ch.qos.logback:logback-classic:1.2.3',
      'jsoup': 'org.jsoup:jsoup:1.11.3',
  ]

  ext.isCiBuild = System.getenv('CI') == 'true'

  repositories {
    mavenCentral()
    google()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.0-rc01'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
    classpath 'org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.45'
    classpath "com.squareup.sqldelight:gradle-plugin:${versions.sqldelight}"
    classpath 'gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:5.0.0'
    classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
  }
}

allprojects {
  apply plugin: 'org.jlleitschuh.gradle.ktlint'

  repositories {
    mavenCentral()
    google()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    jcenter()
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all Kotlin stdlib artifacts to use the same version.
        if (details.requested.group == 'org.jetbrains.kotlin' && details.requested.name.startsWith('kotlin-stdlib')) {
          details.useVersion versions.kotlin
        }
      }
    }
  }

  // TODO remove once https://github.com/Kotlin/kotlin-frontend-plugin/issues/141 is fixed
  plugins.whenPluginAdded { plugin ->
    if (plugin.class.name == 'org.jetbrains.kotlin.gradle.frontend.FrontendPlugin') {
      def fixTask = tasks.register('webpack-config-fix') {
        def dir = file('webpack.config.d')
        it.outputs.dir(dir)
        it.doLast { dir.mkdir() }
      }
      afterEvaluate {
        tasks.named('webpack-config').configure {
          it.dependsOn(fixTask)
        }
      }
    }
  }

  plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
    plugin.extension.compileOptions {
      sourceCompatibility = JavaVersion.VERSION_1_8
      targetCompatibility = JavaVersion.VERSION_1_8
    }
  }

  tasks.withType(JavaCompile).configureEach { task ->
    task.sourceCompatibility = JavaVersion.VERSION_1_8
    task.targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
    task.kotlinOptions {
      freeCompilerArgs += [
          '-XXLanguage:+NewInference',
          '-progressive',
          '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
          '-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi',
      ]
    }
  }

  tasks.withType(Test) {
    testLogging {
      events "skipped", "failed", "passed"
    }
  }
  // TODO same thing for instrumentation tests: https://issuetracker.google.com/issues/37056080
  //tasks.withType(AndroidTestTask) {
  //  testLogging {
  //    events "skipped", "failed", "passed"
  //  }
  //}
}

def gitSha() {
  def f = new File(buildDir, "commit-sha.txt")
  if (!f.exists()) {
    def p = 'git rev-parse HEAD'.execute([], project.rootDir)
    if (p.waitFor() != 0) {
      throw new RuntimeException(p.errorStream.text)
    }
    f.parentFile.mkdirs()
    f.text = p.text.trim()
  }
  return f.text.trim()
}

def gitTimestamp() {
  def f = new File(buildDir, "commit-timestamp.txt")
  if (!f.exists()) {
    def p = 'git log -n 1 --format=%at'.execute([], rootDir)
    if (p.waitFor() != 0) {
      throw new RuntimeException(p.errorStream.text)
    }
    f.parentFile.mkdirs()
    f.text = p.text.trim()
  }
  return f.text.trim()
}

def propOrEmpty(String name) {
  return hasProperty(name) ? getProperty(name) : ''
}

apply from: file('gradle/projectDependencyGraph.gradle')
