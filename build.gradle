buildscript {
  ext.versions = [
      'supportLibrary': '25.1.0',
      'kotlin': '1.1-M04'
  ]

  repositories {
    mavenCentral()
    jcenter()
    maven {
      url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1'
    }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.0-beta1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  defaultConfig {
    applicationId 'com.jakewharton.sdksearch'

    minSdkVersion 24
    targetSdkVersion 25

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }

  signingConfigs {
    debug {
      storeFile rootProject.file('debug.keystore')
      storePassword 'sdksearch'
      keyAlias 'sdksearch'
      keyPassword 'sdksearch'
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      signingConfig signingConfigs.debug
    }
  }
}

repositories {
  mavenCentral()
  jcenter()
  maven {
    url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1'
  }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"

  compile "com.android.support:support-annotations:${versions.supportLibrary}"
  compile "com.android.support:support-v4:${versions.supportLibrary}"
  compile "com.android.support:appcompat-v7:${versions.supportLibrary}"
  compile "com.android.support:design:${versions.supportLibrary}"
  compile "com.android.support:recyclerview-v7:${versions.supportLibrary}"
  compile "com.android.support:customtabs:${versions.supportLibrary}"
  compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

  compile 'com.squareup.okio:okio:1.11.0'

  compile 'com.squareup.moshi:moshi:1.3.1'

  compile 'com.squareup.okhttp3:okhttp:3.5.0'
  compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'

  compile 'com.squareup.retrofit2:retrofit:2.1.0'
  compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
  compile 'com.squareup.retrofit2:converter-moshi:2.1.0'

  compile 'io.reactivex:rxjava:1.2.4'
  compile 'com.artemzin.rxjava:proguard-rules:1.2.4.0'
  compile 'io.reactivex:rxandroid:1.2.0'
  compile 'com.jakewharton.rxrelay:rxrelay:1.2.0'
  compile 'com.jakewharton.rxbinding:rxbinding-kotlin:1.0.0'
  compile 'com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:1.0.0'

  compile 'com.jakewharton.timber:timber:4.4.0'

  compile 'com.google.dagger:dagger:2.8'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.8'

  compile 'com.jakewharton:butterknife:8.4.0'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}

// Force all of the primary support libraries to use the same version.
configurations.all {
  resolutionStrategy {
    eachDependency { details ->
      if (details.requested.group == 'com.android.support') {
        details.useVersion versions.supportLibrary
      }
    }
  }
}
