apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'

archivesBaseName = 'sync'

// TODO work around https://youtrack.jetbrains.com/issue/KT-27170
configurations {
  compileClasspath
}

kotlin {
  sourceSets {
    commonMain {
      dependencies {
        api project(':api:dac')
        api project(':store:item')
        api deps.kotlin.stdlib.common
        api deps.kotlin.coroutines.common
        implementation deps.timber.common
      }
    }
    jsMain {
      dependencies {
        api deps.kotlin.stdlib.js
        api deps.kotlin.coroutines.js
        implementation deps.timber.js
      }
    }
  }

  targets {
    fromPreset(presets.android, 'android')
    fromPreset(presets.js, 'js')
  }
}

/* android */ dependencies {
  // TODO these two shouldn't be required to be duplicated here
  api project(':api:dac')
  api project(':store:item')

  api project(':api:dac:jdk-temp')

  api deps.kotlin.stdlib.jdk
  api deps.kotlin.coroutines.jdk
  implementation deps.timber.jdk
}

android {
  compileSdkVersion buildConfig.compileSdk

  defaultConfig {
    minSdkVersion buildConfig.minSdk
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    lintConfig rootProject.file('lint.xml')

    // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
    checkReleaseBuilds false
  }
}
