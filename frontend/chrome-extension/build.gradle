apply plugin: 'kotlin2js' // TODO why isn't this org.jetbrains.kotlin.js?
apply plugin: 'kotlin-dce-js'
apply plugin: 'com.eriwen.gradle.js'
apply plugin: 'distribution'

archivesBaseName = 'sdk-search'

dependencies {
  compile deps.kotlin.stdlib.js
  compile project(':references:js')
}

combineJs {
  source = [
      // Order is important!
      "$buildDir/kotlin-js-min/main/kotlin.js",
      "$buildDir/kotlin-js-min/main/references.js",
      "$buildDir/kotlin-js-min/main/sdk-search.js",
  ]
  dest = file("$buildDir/${archivesBaseName}.js")
}
combineJs.dependsOn(runDceKotlinJs)

distZip.dependsOn(combineJs)
distTar.enabled = false
installDist.dependsOn(combineJs)

distributions {
  main {
    baseName = archivesBaseName

    contents {
      from 'manifest.json'
      from 'hello.html'
      from "$buildDir/${archivesBaseName}.js"
    }
  }
}
