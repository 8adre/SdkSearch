apply plugin: 'kotlin2js' // TODO why isn't this org.jetbrains.kotlin.js?
apply plugin: 'kotlin-dce-js'
apply plugin: 'com.eriwen.gradle.js'
apply plugin: 'distribution'

archivesBaseName = 'sdk-search'

dependencies {
  compile deps.kotlin.stdlib.js
  compile deps.kotlin.coroutines.js
  compile deps.kotlin.serialization.js
  compile project(':api:dac:js')
  compile project(':references:js')
  compile project(':chrome-platform')
}

kotlin {
  experimental {
    coroutines 'enable'
  }
}

combineJs {
  source = [
      // Order is important!
      "$buildDir/kotlin-js-min/main/kotlin.js",
      "$buildDir/kotlin-js-min/main/kotlinx-coroutines-core-js.js",
      "$buildDir/kotlin-js-min/main/kotlinx-serialization-runtime-js.js",
      "$buildDir/kotlin-js-min/main/api-dac.js",
      "$buildDir/kotlin-js-min/main/references.js",
      "$buildDir/kotlin-js-min/main/chrome-platform.js",
      "$buildDir/kotlin-js-min/main/sdk-search.js",
  ]
  dest = file("$buildDir/combined/${archivesBaseName}.js")
}
combineJs.dependsOn(runDceKotlinJs)

distZip.dependsOn(combineJs)
distTar.enabled = false
installDist.dependsOn(combineJs)

distributions {
  main {
    baseName = archivesBaseName

    contents {
      from 'src/main/manifest.json'
      from 'src/main/static'
      from "$buildDir/combined"
    }
  }
}
