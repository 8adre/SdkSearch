apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'com.eriwen.gradle.js'

dependencies {
  compile deps.kotlin.stdlib.js
  compile project(':store:config:js')
  compile project(':store:item:js')
  compile project(':references:js')
}

kotlin {
  experimental {
    coroutines 'enable'
  }
}

combineJs {
  source = [
          // Order is important!
          "$buildDir/kotlin-js-min/main/kotlin.js",
          "$buildDir/kotlin-js-min/main/kotlinx-coroutines-core.js",
          "$buildDir/kotlin-js-min/main/chrome-platform.js",
          "$buildDir/kotlin-js-min/main/references.js",
          "$buildDir/kotlin-js-min/main/store-config.js",
          "$buildDir/kotlin-js-min/main/content.js",
  ]
  dest = file("$buildDir/combined/${archivesBaseName}.js")
}
combineJs.dependsOn(runDceKotlinJs)

configurations {
  content
}

artifacts {
  content(combineJs.dest) {
    builtBy combineJs
  }
}
