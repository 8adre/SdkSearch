apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'com.eriwen.gradle.js'

dependencies {
  compile deps.kotlin.stdlib.js
  compile deps.kotlin.coroutines.js
  compile project(':options:ui-web')
  compile project(':store:config:js-storage-area')
}

kotlin {
  experimental {
    coroutines 'enable'
  }
}

combineJs {
  source = [
          // Order is important!
          "$buildDir/kotlin-js-min/main/kotlin.js",
          "$buildDir/kotlin-js-min/main/atomicfu.js",
          "$buildDir/kotlin-js-min/main/kotlinx-coroutines-core.js",
          "$buildDir/kotlin-js-min/main/chrome-platform.js",
          "$buildDir/kotlin-js-min/main/references.js",
          "$buildDir/kotlin-js-min/main/sqldelight-runtime-js.js",
          "$buildDir/kotlin-js-min/main/store-config.js",
          "$buildDir/kotlin-js-min/main/options-presenter.js",
          "$buildDir/kotlin-js-min/main/options-ui-web.js",
          "$buildDir/kotlin-js-min/main/options.js",
  ]
  dest = file("$buildDir/combined/${archivesBaseName}.js")
}
combineJs.dependsOn(runDceKotlinJs)

configurations {
  content
}

artifacts {
  content(combineJs.dest) {
    builtBy combineJs
  }
  content file('options.html')
}
