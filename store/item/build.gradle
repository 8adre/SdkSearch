apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.squareup.sqldelight'

archivesBaseName = 'store-item'

// TODO work around https://youtrack.jetbrains.com/issue/KT-27170
configurations {
  compileClasspath
}

kotlin {
  jvm()
  js {
    compilations.main.kotlinOptions {
      moduleKind = 'umd'
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        api deps.kotlin.stdlib.common
        api deps.kotlin.coroutines.common
        api deps.sqldelight.runtime.common
      }
    }
    jsMain {
      dependencies {
        api deps.kotlin.stdlib.js
        api deps.kotlin.coroutines.js
        api deps.sqldelight.runtime.js
      }
    }
    jvmMain {
      dependencies {
        api deps.kotlin.stdlib.jdk
        api deps.kotlin.coroutines.jdk
        api deps.sqldelight.runtime.jdk
      }
    }
  }
}

sqldelight {
  ItemDatabase {
    packageName = 'com.jakewharton.sdksearch.store.item'
  }
}

ktlint {
  ignoreFailures = true // TODO why isn't this working?

  filter {
    exclude("**/sqldelight/**")
  }
}
