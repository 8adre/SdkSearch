apply plugin: 'org.jetbrains.kotlin.platform.js'
apply plugin: 'com.moowork.node'

archivesBaseName = 'store-item'

dependencies {
  expectedBy project(':store:item')

  compile deps.kotlin.stdlib.js
  compile deps.kotlin.coroutines.js
  compile deps.sqldelight.runtime.js

  testCompile deps.kotlin.test.js
}

compileKotlin2Js {
  kotlinOptions.moduleKind = 'commonjs'
}

compileTestKotlin2Js {
  kotlinOptions.moduleKind = 'commonjs'
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
  from compileKotlin2Js.destinationDir
  configurations.testCompile.each {
    from zipTree(it.absolutePath).matching { include '*.js' }
  }

  into "$buildDir/node_modules"
}

task installTestRunner(type: NpmTask) {
  args = ['install', 'mocha']
}

task runTests(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installTestRunner]) {
  script = file('node_modules/.bin/mocha')
  args = [projectDir.toPath().relativize(file(compileTestKotlin2Js.outputFile).toPath())]
}

test.dependsOn(runTests)
